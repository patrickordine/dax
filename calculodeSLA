#TempoUtilDesligamento = 

// Variáveis Iniciais
VAR _criado = 'Desligamento - RH Phase History'[firstTimeIn]
VAR _finalizado = if('Desligamento - RH Phase History'[lastTimeOut]=BLANK(),now(),'Desligamento - RH Phase History'[lastTimeOut])

//Variaveis de Horário de Expediente
VAR _inicio = TIMEVALUE("07:00:00")
VAR _fim = TIMEVALUE("17:00:00")

// Contagem de dias Úteis e Folgas
VAR _diasUteis = NETWORKDAYS(DATEVALUE(_criado), DATEVALUE(_finalizado))
VAR _diasFolga = (DATEDIFF(_criado, _finalizado, DAY) + 1) - _diasUteis
VAR _dias = DATEDIFF(DATEVALUE(_criado),DATEVALUE(_finalizado),DAY)+1
VAR _mesmoDia = DATEVALUE(_criado)=DATEVALUE(_finalizado)

// Verificações de Dias Uteis e Finais de Semana
VAR _criadoUtil = (WEEKDAY(DATEVALUE(_criado),3)<5)
VAR _criadoNoSabado = (WEEKDAY(DATEVALUE(_criado),3)=5)
VAR _criadoNoDomingo =(WEEKDAY(DATEVALUE(_criado),3)=6)
VAR _criadoNoFinalDeSemana = _criadoNoSabado || _criadoNoDomingo
VAR _finalizadoUtil = (WEEKDAY(DATEVALUE(_finalizado),3)<5)
VAR _finalizadoNoSabado = (WEEKDAY(DATEVALUE(_finalizado),3)=5)
VAR _finalizadoNoDomingo = (WEEKDAY(DATEVALUE(_finalizado),3)=6)
VAR _finalizadoNoFinalDeSemana = _finalizadoNoSabado || _finalizadoNoDomingo

//Tempo entre datas
VAR _tempoEntreCriacaoEFim = if(_diasUteis=1, 0, IF(_diasUteis=2 && _criadoNoSabado,10, IF(_diasUteis=2 && _finalizadoNoSabado,10,if(_diasUteis=3 && _criadoNoSabado,20,if(_criadoNoFinalDeSemana || _finalizadoNoFinalDeSemana,((_dias-1)-_diasFolga)*10,((_dias-2)-_diasFolga)*10)))))

//Calculo de horas Uteis no primeiro dia
VAR _primeiroDia =  if(_criadoNoFinalDeSemana,0, IF(TIMEVALUE(_criado)>Time(HOUR(_fim),0,0),0,DATEDIFF(TIMEVALUE(_criado),Time(hour(_fim),0,0),MINUTE)/60))

//Calculo de horas uteis no último dia
VAR _ultimoDia = IF(_finalizadoNoFinalDeSemana,0,if(TIMEVALUE(_finalizado)>Time(hour(_fim),0,0),10,DATEDIFF(TIME(HOUR(_inicio),0,0),TIMEVALUE(_finalizado),MINUTE)/60))

//Calculo do tempo útil da fase
VAR _tempo = 
    // Criado e Finalizado no mesmo dia E em dia Útil
    IF(_mesmoDia && _criadoUtil, _primeiroDia,

    // Criado no final de semana e finalizado em dia Útil
    IF(_criadoNoFinalDeSemana && _finalizadoUtil, _tempoEntreCriacaoEFim +_ultimoDia + _primeiroDia,
    
    //Criado Fora do Expediente em dia Útil
    IF(_mesmoDia, _primeiroDia,

    //Restante
        _tempoEntreCriacaoEFim
        +
        _primeiroDia
        +
        _ultimoDia 
    )))
    
RETURN
if(_finalizado=BLANK()||_criado=BLANK(),BLANK(),IF(_tempo<0,0,_tempo))
